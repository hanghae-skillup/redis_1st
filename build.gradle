plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.example'
    version = '0.0.1-SNAPSHOT'
    java.sourceCompatibility = JavaVersion.VERSION_17

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        implementation 'org.springframework.boot:spring-boot-starter-batch'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-starter-web'

        // Doc
        implementation 'io.swagger.core.v3:swagger-jaxrs2:2.2.7'
        implementation 'io.swagger.core.v3:swagger-jaxrs2-servlet-initializer-v2:2.2.7'

        // QueryDSL
        implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
        annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
        annotationProcessor 'jakarta.persistence:jakarta.persistence-api'
        annotationProcessor 'jakarta.annotation:jakarta.annotation-api'

        // Lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        // Domain Validation
        implementation 'com.google.guava:guava:33.4.0-jre'
        implementation 'commons-validator:commons-validator:1.9.0'

        runtimeOnly 'com.mysql:mysql-connector-j'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

tasks.named('test') {
    dependsOn ':domain:test', ':application:test', ':api:test'
}

tasks.named('wrapper') {
    gradleVersion = '8.11.1'
}

project(":domain") {
    dependencies {
        implementation project(":common")
    }
}

project(":application") {
    dependencies {
        implementation project(":common")
        implementation project(":domain")
    }
}

project(":api") {
    dependencies {
        implementation project(":common")
        implementation project(":domain")
        implementation project(":application")
    }
}